<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns="http://www.w3.org/1999/xhtml" xmlns:giweet="http://www.giweet.org/javadoc/1.0">
	<xsl:output method="html" 
				encoding="utf-8" 
				omit-xml-declaration="yes" 
				doctype-system="http://www.w3.org/TR/html4/loose.dtd" 
				doctype-public="-//W3C//DTD HTML 4.01 Transitional//EN"/>
	<xsl:param name="generationDate"/>
	<xsl:param name="stylesheetfile"/>
	
	<xsl:variable name="lang.generation.info" select="'Generation date: '"/>
	<xsl:variable name="lang.java.class" select="'Documentation generated from Java class: '"/>
	<xsl:variable name="lang.steps" select="'Test steps'"/>
	<xsl:variable name="lang.available.steps" select="'Available steps are: '"/>
	<xsl:variable name="lang.steps.description" select="'Steps description'"/>
	<xsl:variable name="lang.step" select="'Step: '"/>
	<xsl:variable name="lang.step.method" select="'This step is resolved by the method '"/>
	
	<xsl:template match="/">
		<html>
			<xsl:comment>Generated by GiweetDoclet on <xsl:value-of select="$generationDate"/></xsl:comment>
			<head>
				<link rel="stylesheet" type="text/css" href="{$stylesheetfile}" />
				<title>Steps from <xsl:value-of select="/giweet:stepClass/giweet:type"/></title>
			</head>
			<body>
				<xsl:apply-templates/>
				<div class="footer">
					<span class="generation-info"><xsl:value-of select="$lang.generation.info"/> <xsl:value-of select="$generationDate"/></span>
				</div>
			</body>
		</html>		
	</xsl:template>
	
	<xsl:template match="giweet:stepClass">
			<h1><xsl:value-of select="$lang.steps"/></h1>
			<div class="doc"><xsl:value-of select="giweet:doc" disable-output-escaping="yes" /></div>
			<div class="generation-info">(<xsl:value-of select="$lang.java.class"/> <xsl:value-of select="giweet:type" />)</div>
			<div class="steps-summary">
				<xsl:text><xsl:value-of select="$lang.available.steps"/></xsl:text>
				<ul class="steps-list">
					<xsl:apply-templates select="giweet:steps" mode="summary"/>
				</ul>
			</div>
			<div class="steps">
				<h2><xsl:value-of select="$lang.steps.description"/></h2>
				<xsl:apply-templates select="giweet:steps"/>
			</div>
	</xsl:template>

	<xsl:template match="giweet:step" mode="summary">
		<li class="step">
			<a href="#{giweet:id}"><xsl:value-of select="giweet:name"/></a>
		</li>
	</xsl:template>

	<xsl:template match="giweet:step">
		<div class="step">
			<a name="{giweet:id}"/>
			<h3><xsl:value-of select="$lang.step"/><span class="step"><xsl:value-of select="giweet:name"/></span></h3>
			<div class="doc"><xsl:value-of select="giweet:doc" disable-output-escaping="yes" /></div>
			<div class="generation-info"><xsl:value-of select="$lang.step.method"/><xsl:value-of select="concat(giweet:class, '#', giweet:method)" /></div>
		</div>
	</xsl:template>

</xsl:stylesheet>